apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

ext {
    versionMajor = 0
    versionMinor = 0
    versionPatch = 1
    versionClassifier = null
    isSnapShot = false
    minSdkVersion = 21
}


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sterlingbankng.football"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName generateVersionName()
        buildConfigField("String", "X_AUTH_TOKEN", "\"e66c101549984d2cafbeb64f0ba42263\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Support libraries
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'

    //Architecture components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha04'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'androidx.room:room-testing:2.1.0-alpha04'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    kapt 'androidx.room:room-compiler:2.1.0-alpha04'

    // Koin
    implementation 'org.koin:koin-android:2.0.0-beta-1'
    implementation 'org.koin:koin-androidx-viewmodel:2.0.0-beta-1'
    androidTestImplementation 'org.koin:koin-test:2.0.0-beta-1'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    //RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    //Glide for Image loading
    implementation 'com.caverock:androidsvg:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    //Google JSON Parser
    implementation 'com.google.code.gson:gson:2.8.5'

    //OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    
    //Test libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
    androidTestImplementation 'androidx.test:rules:1.1.2-alpha01'
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 +
            ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapShot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-${ext.versionClassifier}"
    }

    return versionName
}
