apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-parcelize'

apply plugin: 'kotlin-kapt'

ext {
    versionMajor = 0
    versionMinor = 0
    versionPatch = 1
    versionClassifier = null
    isSnapShot = false
    minSdkVersion = 21
}


android {
    compileSdk 35
    defaultConfig {
        applicationId "com.sterlingbankng.football"
        minSdkVersion project.ext.minSdkVersion
        targetSdk 34
        versionCode generateVersionCode()
        versionName generateVersionName()
        buildConfigField "String", "X_AUTH_TOKEN", API_KEY
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    namespace "com.sterlingbankng.football"

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Support libraries
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout

    //Kotlin
    implementation libs.kotlin.stdlib.jdk7
    implementation libs.androidx.core.ktx

    //Architecture components
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.rxjava2
    testImplementation libs.androidx.core.testing
    testImplementation libs.androidx.room.testing
    kapt libs.androidx.lifecycle.compiler
    kapt libs.androidx.room.compiler

    // Koin
    implementation libs.koin.android
    implementation libs.koin.androidx.viewmodel
    androidTestImplementation libs.koin.test

    //Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.retrofit.adapter.rxjava2

    //RxAndroid
    implementation libs.rxjava.rxandroid

    //Glide for Image loading
    implementation libs.androidsvg
    implementation libs.glide
    kapt libs.glide.compiler

    //Google JSON Parser
    implementation libs.gson

    //OkHttp
    implementation platform(libs.okhttp.bom)
    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation libs.androidx.legacy.support.v4
    
    //Test libraries
    testImplementation libs.junit
    androidTestImplementation libs.androidx.runner
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.rules
    // Kotlin extensions for androidx.test.ext.junit
    androidTestImplementation libs.androidx.junit.ktx
}

private Integer generateVersionCode() {
    return ext.minSdkVersion * 10000000 + ext.versionMajor * 10000 +
            ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapShot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-${ext.versionClassifier}"
    }

    return versionName
}
